plugins {
    id "io.micronaut.build.internal.views-module"
    id "com.github.node-gradle.node" version "7.0.2"
}

dependencies {
    annotationProcessor(mnValidation.micronaut.validation.processor)

    api projects.micronautViewsCore
    api libs.jetbrains.annotations
    implementation(mn.micronaut.http.client)

    // The user of this library is expected to supply the JS dependency. That's because they may choose between
    // the community edition (open source, slower) and the enterprise edition (liberal usage, not open source, faster).
    implementation(libs.graal.polyglot)

    compileOnly(mn.micronaut.management)
    compileOnly(mnValidation.micronaut.validation)
    compileOnly(mn.micronaut.http)

    testCompileOnly(mn.micronaut.inject.groovy)
    testAnnotationProcessor(mnValidation.micronaut.validation.processor)
    testAnnotationProcessor(mn.micronaut.inject.java)

    testImplementation(mnSerde.micronaut.serde.jackson)
    testImplementation(mn.micronaut.http.client)
    testImplementation(mn.micronaut.http.server.netty)
    testImplementation(mn.micronaut.management)
    testImplementation(mnValidation.micronaut.validation)
    testImplementation(mn.snakeyaml)

    // We will use enterprise edition for testing.
    testImplementation(libs.graal.js)
}

// Set up a simple SSR project for testing purposes. We will need nodejs to run the bundlers.
node {
    download = true
    version = "21.7.1"
    nodeProjectDir = file("${project.projectDir}/src/test/js")
}

task buildTestClientJS(type: NpxTask) {
    dependsOn npmInstall
    command = 'webpack'
    args = ["--mode", "development", "--config", "webpack.client.js"]
    inputs.dir(fileTree("src/test/js/").exclude(".cache"))
    outputs.file('src/test/resources/views/static/client.js')
}

task buildTestServerJS(type: NpxTask) {
    dependsOn npmInstall
    command = 'webpack'
    args = ["--mode", "development", "--config", "webpack.server.js"]
    inputs.dir(fileTree("src/test/js/").exclude(".cache"))
    outputs.file('src/test/resources/views/ssr-components.mjs')
}

def buildTestJS = tasks.register("buildTestJS") {
    it.dependsOn("buildTestClientJS", "buildTestServerJS")
}

tasks.named("processTestResources") {
    it.dependsOn(buildTestJS)
}

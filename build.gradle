buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build.internal:micronaut-gradle-plugins:3.0.4"
    }
}

subprojects { Project subproject ->

    group "io.micronaut.views"

    // TODO: Remove this when using a non-snapshot version of core
    repositories {
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    }

    if (subproject.name != 'views-bom') {
        apply plugin: "io.micronaut.build.internal.common"
        micronautBuild {
            enableBom = false
        }
        dependencies {
            annotationProcessor "io.micronaut:micronaut-inject-java:$micronautVersion"
            if (!subproject.name.startsWith("test-suite")) {
                annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:$micronautDocsVersion"
            }
            testImplementation("org.spockframework:spock-core:${spockVersion}") {
                exclude module: 'groovy-all'
            }
            testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
        }
    } else {
        apply plugin: "java-platform"
        version projectVersion
    }
    apply plugin: "io.micronaut.build.internal.dependency-updates"
    if (!subproject.name.startsWith("test-suite")) {
        apply plugin: "io.micronaut.build.internal.publishing"
    }
}

apply plugin: "io.micronaut.build.internal.docs"
apply plugin: "io.micronaut.build.internal.dependency-updates"

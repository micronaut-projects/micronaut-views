buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.10"
    }
}



subprojects { Project subproject ->

    group "io.micronaut.views"

    repositories {
        jcenter()
    }

    if (subproject.name != 'views-bom') {
        apply plugin: "io.micronaut.build.common"

        micronautBuild {
            enforcedPlatform = true
            enableBom = false
        }

    } else {
        apply plugin: "java-platform"
        version projectVersion
    }

    if (subproject.parent.name == "docs-examples" || subproject.name == "docs-examples") {
        return
    }

    configurations.all {
        resolutionStrategy.force "org.spockframework:spock-core:${spockVersion}"
    }
    
    apply plugin: "io.micronaut.build.publishing"

    if (subproject.name != 'views-bom') {
        
        apply plugin: "io.micronaut.build.dependency-updates"
        dependencies {
            testImplementation("org.spockframework:spock-core:${spockVersion}") {
                exclude module:'groovy-all'
            }
            testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"        
            // testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.6.2'     
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'     
        }       

    }   
}

apply plugin: "io.micronaut.build.docs"
apply plugin: "io.micronaut.build.dependency-updates"

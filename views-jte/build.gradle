plugins {
    id("io.micronaut.build.internal.module")
    id 'gg.jte.gradle' version "$jteVersion"
}

dependencies {
    api "gg.jte:jte:$jteVersion"
    api project(":views-core")
    testImplementation "io.projectreactor:reactor-core"
    testImplementation "io.micronaut:micronaut-http-client:$micronautVersion"
    testImplementation "io.micronaut:micronaut-http-server-netty:$micronautVersion"
}

jte {
    sourceDirectory = file('src/test/resources/views').toPath()
    generate()
}

// Workaround because jte gradle plugin currently hardcodes adding generated sources to 'main'
// sourceset. In this project they _should_ be a dependency of 'test'.
['jar', 'sourcesJar'].each { name ->
    tasks.getByName(name) {
        dependsOn generateJte
        exclude "gg"
    }
}

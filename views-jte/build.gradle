plugins {
    id "io.micronaut.build.internal.views-module"
    alias(libs.plugins.jte)
}

dependencies {
    compileOnly(mn.micronaut.http)

    api(libs.managed.jte)

    api projects.micronautViewsCore
    implementation(libs.managed.jte.kotlin)
    testImplementation(mnSerde.micronaut.serde.jackson)
    testImplementation(mn.reactor)
    testImplementation(mn.micronaut.http.client)
    testImplementation(mn.micronaut.http.server.netty)
    testImplementation(projects.micronautViewsTurbo)
}

jte {
    sourceDirectory = file('src/test/resources/views').toPath()
    generate()
}

// Workaround because jte gradle plugin currently hardcodes adding generated sources to 'main'
// sourceset. In this project they _should_ be a dependency of 'test'.
['jar', 'sourcesJar'].each { name ->
    tasks.getByName(name) {
        dependsOn generateJte
        exclude "gg"
    }
}

tasks.getByName("prepareJavadocAggregation") {
    dependsOn generateJte
}

